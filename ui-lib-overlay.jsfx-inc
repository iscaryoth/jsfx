@init
uix_default_fontface = "Verdana";

function gfx_ui_knob(slidervar*, low, high, step, title, format) (
  ui_split_top(10);
  ui_pop();
  ui_split_top(30);
    ui_color_text();
    ui_text(title); 
  ui_pop();
  ui_split_bottom(20);      
    ui_color_accent_dim();
    sprintf(#uix_knob_value, format, slidervar);
    ui_text(#uix_knob_value); 
  ui_pop();
  
  control_dial(slidervar, low, high, 0);
);

function gfx_gradhelper(x,y,w,fh,h1,h2,r1,g1,b1,r2,g2,b2) local(dd dr dg db s1 s2) (
  dd = 1/fh;
  dr = (r2-r1) * dd;
  dg = (g2-g1) * dd;
  db = (b2-b1) * dd;
  
  s1 = h1;
  s2 = h2/fh;
  gfx_gradrect(x,y+h1,w,h2-h1, r1 + s1*dr,g1 + s1*dg, b1 + s1*db,1, 0,0,0,0, dr,dg,db,0);
);

function  gfx_ui_meter_calc_relative_pos(value) local(p) (
  p = 1 - ((value - vu.meter_ceil) / (vu.meter_floor - vu.meter_ceil));
  p = max(0, p);
  p = min(1, p);
  p;
);

function gfx_ui_meter(value) local(p h fh) (
  p = gfx_ui_meter_calc_relative_pos(value);
  h = ui_height() * p;
  fh = ui_height();
  
  //1.000, 0.165, 0.027, 0
  //0.470, 0.910, 0.322, 0
  //0.002, 0.996, 0.576, 0
  gfx_gradhelper(ui_left(), ui_bottom()-fh,   ui_width(), fh/2, 0, fh/2, 1.000, 0.165, 0.027, 0.470, 0.910, 0.322);
  gfx_gradhelper(ui_left(), ui_bottom()-fh/2, ui_width(), fh/2, 0, fh/2, 0.470, 0.910, 0.322, 0.002, 0.996, 0.576); 
  gfx_set(0.078,0.078,0.078);
  gfx_rect(ui_left(), ui_top(), ui_width(), ui_height() * (1-p));
);

function gfx_ui_margins(top, right, bottom, left) (
  ui_split_top(top);
  ui_pop();
  ui_split_bottom(bottom);
  ui_pop();
  ui_split_right(right);
  ui_pop();
  ui_split_left(left);
  ui_pop();
);

function gfx_ui_stereometer(name) local(sumvalue valueClipped) (
  ui_split_top(30);
    ui_text(name);
  ui_pop();
  
  ui_split_bottom(30);
    sumvalue = this.sum.value;
    valueClipped = (sumvalue < vu.meter_floor ? vu.meter_floor : sumvalue);
    sprintf(#uix_meter_value, "%.1f", valueClipped);
    ui_text(#uix_meter_value); 
  ui_pop();
  
  ui_split_left(10);
  ui_pop();
  ui_split_right(10);
  ui_pop();
  gfx_set(0.078,0.078,0.078);
  gfx_rect(ui_left(), ui_top(), ui_width(), ui_height());
  ui_split_leftratio(1/2);
    gfx_ui_margins(1, 0.5,1, 0.5);
    gfx_ui_meter(this.l.value);
  ui_split_next();
    gfx_ui_margins(1, 0.5,1, 0.5);
    gfx_ui_meter(this.r.value);
  ui_pop();
);



function gfx_ui_meter_numbers(x,y,w,h) local(i, scale, cv, num, cy, offset) (
  num = 10;
  scale = (vu.meter_floor - vu.meter_ceil) / (num + 1);
  cv = 0;
  i = 1;
  ui_color(140, 140, 140);
  while(
    cy = y + i * h / (num+1);
    gfx_line(x + 35, cy, x+w-25, cy, 1);
    sprintf(#uix_meter_scale, "%.0f", abs(cv));
    gfx_x = ui_left() + 5;
    gfx_y = cy-10;
    gfx_drawstr(#uix_meter_scale, 2+4, x + 30, cy+10);
    cv += scale;
    i += 1;
    i <= num;
  )
);

